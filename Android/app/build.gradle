apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'

def clientId() {
  return hasProperty('CLIENT_ID') ? CLIENT_ID : ''
}

def clientSecret() {
  return hasProperty('CLIENT_SECRET') ? CLIENT_SECRET : ''
}

def keystorePass() {
  return hasProperty('KEYSTORE_PASS') ? KEYSTORE_PASS : ''
}

def versionMajor = 0
def versionMinor = 1
def versionPatch = 0
def versionBuild = 0

android {
  compileSdkVersion 23
  buildToolsVersion "23.0.2"

  defaultConfig {
    applicationId "com.zestedesavoir.zdsnotificateur"
    minSdkVersion 15
    targetSdkVersion 23

    versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
    versionName "${versionMajor}.${versionMinor}.${versionPatch}"

    buildConfigField "String", "CLIENT_ID", "\"${clientId()}\""
    buildConfigField "String", "CLIENT_SECRET", "\"${clientSecret()}\""
  }

  signingConfigs {
    release {
      storeFile file("znotificateur.keystore")
      storePassword keystorePass()
      keyAlias 'znotificateur'
      keyPassword keystorePass()
    }
    beta {
      storeFile file("beta.keystore")
      storePassword 'android'
      keyAlias 'beta'
      keyPassword 'android'
    }
  }

  buildTypes {
    release {
      signingConfig signingConfigs.release
    }
    beta {
      applicationIdSuffix '.beta'
      signingConfig signingConfigs.beta
    }
    debug {
      applicationIdSuffix '.debug'
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
}

dependencies {
  testCompile 'junit:junit:4.12'
  compile 'com.android.support:appcompat-v7:23.3.0'
  compile 'com.android.support:design:23.3.0'
  compile 'com.android.support:recyclerview-v7:23.3.0'
  compile 'com.android.support:support-v4:23.3.0'

  compile 'com.squareup.retrofit:retrofit:2.0.0-beta1'
  compile 'com.squareup.retrofit:converter-gson:2.0.0-beta1'
  compile 'com.squareup.sqlbrite:sqlbrite:0.5.0'
  compile 'com.squareup.dagger:dagger:1.2.2'
  apt 'com.squareup.dagger:dagger-compiler:1.2.2'

  compile 'com.jakewharton.timber:timber:4.1.0'
  compile 'com.jakewharton:butterknife:8.2.1'
  apt 'com.jakewharton:butterknife-compiler:8.2.1'

  compile 'net.danlew:android.joda:2.9.1'
  compile 'de.hdodenhof:circleimageview:2.1.0'
  compile 'com.github.bumptech.glide:glide:3.7.0'
}
